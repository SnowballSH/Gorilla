program = _{ SOI ~ TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~ EOI }

TERMINATOR = _{ NEWLINE | ";" }
WHITESPACE = _{ " " | "\t" }

integer = @{ ASCII_DIGIT{1, 18} }
identifier = @{ !keyword ~ (ASCII_ALPHA | "_" | "$") ~ (ASCII_ALPHANUMERIC | "_")* }
keyword = @{
    "if" | "else"
}

call = {
    "(" ~ (expression ~ ",")* ~ expression? ~ ")"
}

empty_call = {
    "!"
}

field = {
    "." ~ identifier
}

suffix = {
    term ~ (call | field | empty_call)+
}

term = _{ integer | identifier | "(" ~ expression ~ ")" }

prefix = { prefix_operators+ ~ expression_inner }

expression_inner = _{ suffix | prefix | term }

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
modulo = { "%" }

operators = _{
    add | sub | mul | div | modulo
}

prefix_operators = _{
    add | sub
}

infix = _{ expression_inner ~ (operators ~ expression_inner)+ }

assign = { identifier ~ "=" ~ expression }

expression = { assign | infix | expression_inner }

expression_stmt = { expression }

stmt = _{
    expression_stmt
}