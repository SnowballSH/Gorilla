program = _{ SOI ~ TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~ EOI }

TERMINATOR = _{ NEWLINE | ";" }
WHITESPACE = _{ " " | "\t" }

integer = @{ ASCII_DIGIT{1, 18} }
identifier = @{ !keyword ~ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
keyword = @{
    "if" | "else"
}

args = {
    "(" ~ (expression ~ ",")* ~ expression? ~ ")"
}

field = {
    "." ~ identifier
}

call = {
    term ~ (args | field)+
}

term = _{ integer | identifier | "(" ~ expression ~ ")" }

expression_inner = _{ call | term }

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
modulo = { "%" }

operators = _{
    add | sub | mul | div | modulo
}

prefix_operators = _{
    add | sub
}

infix = _{ expression_inner ~ (operators ~ expression_inner)+ }

prefix = { prefix_operators+ ~ expression_inner }

assign = { identifier ~ "=" ~ expression }

expression = { assign | infix | prefix | expression_inner }

expression_stmt = { expression }

stmt = _{
    expression_stmt
}