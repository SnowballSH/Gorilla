program = _{ SOI ~ TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~ EOI }

TERMINATOR = _{ NEWLINE | ";" }
WHITESPACE = _{ " " | "\t" }
block_comment = _{ "#*" ~ (block_comment | !"*#" ~ ANY)* ~ "*#" }
COMMENT = _{ block_comment | ("#" ~ (!NEWLINE ~ ANY)*) }

integer = @{ ASCII_DIGIT{1, 18} }
identifier = @{ !keyword ~ (ASCII_ALPHA | "_" | "$") ~ (ASCII_ALPHANUMERIC | "_")* }
keyword = @{
    "if" | "else" | "fn"
}

string_literal = @{ "\"" ~ literal_char* ~ "\"" }
literal_char = { escape_sequence | (!"\"" ~ ANY) }
escape_sequence = _{ "\\\\" | "\\\"" | "\\\'" | "\\n" | "\\r" | "\\t" | "\\0" }

block = {
    ("{" ~ TERMINATOR* ~ (stmt ~ TERMINATOR+) * ~ stmt? ~  "}") | stmt
}

condition_if = {
    "if" ~ NEWLINE* ~ expression ~ NEWLINE* ~ block
}

condition_ifelse = {
    "if" ~ NEWLINE* ~ expression ~ NEWLINE* ~ block ~ NEWLINE* ~ "else" ~ NEWLINE* ~ block
}

function_declare = {
    "fn" ~ NEWLINE* ~ identifier ~ NEWLINE* ~ function_params ~ NEWLINE* ~ block
}

call = {
    "(" ~ NEWLINE* ~ (expression ~ NEWLINE* ~ "," ~ NEWLINE*)* ~ NEWLINE* ~ expression? ~ NEWLINE* ~ ")"
}

function_params = {
    "(" ~ NEWLINE* ~ (identifier~ NEWLINE*  ~ "," ~ NEWLINE*)* ~ NEWLINE* ~ identifier? ~ NEWLINE* ~ ")"
}

empty_call = {
    !neq ~ "!"
}

field = {
    "." ~ identifier
}

suffix = {
    term ~ (call | field | empty_call)+
}

term = _{ string_literal | integer | identifier | "(" ~ expression ~ ")" }

prefix = { prefix_operators+ ~ expression_inner }

expression_inner = _{ suffix | prefix | term }

add = { "+" }
sub = { "-" }
mul = { "*" }
div = { "/" }
modulo = { "%" }
dbeq = { "==" }
neq = { "!=" }
gt = { ">" }
lt = { "<" }
gteq = { ">=" }
lteq = { "<=" }

operators = _{
    add | sub | mul | div | modulo
    | dbeq | neq | gteq | lteq | gt | lt
}

prefix_operators = _{
    add | sub
}

infix = _{ expression_inner ~ (operators ~ expression_inner)+ }

assign = { identifier ~ "=" ~ expression }

// condition_ifelse must be before condition_if
expression = { assign | condition_ifelse | condition_if | infix | expression_inner }

expression_stmt = { expression }

stmt = _{
    expression_stmt | function_declare
}